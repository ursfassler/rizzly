
interface
  Mode
    function setTime( seconds: R{0,86399} );
    function showTime( seconds: R{0,86399} );
  end
  
  TimeDisplay
    function time( hour: R{0,23}; minute, second: R{0,59} );
  end  
  
  Time
    function time( seconds: R{0,86399} );
  end
  
  TimeConfig
    function time( seconds: R{0,86399} );
    function hasValidTime():Boolean;
    function getTime():R{0,86399};
  end
  
  GenericEvent
    function evt();
  end  
  
   Switch
    function switch( value: Boolean );
  end  
  
component
  Ui
    input
      time      : Time;
      control   : Mode;
      config    : TimeConfig;
      hour      : GenericEvent;
      minute    : GenericEvent;
      tick      : GenericEvent;
    
    output
      timeDisp  : TimeDisplay;
      visible   : Switch;

  implementation hfsm( Top )
    state Top( ShowTime )
      var
        shour:    R{0,23};
        smin:     R{0,59};
      
      function config.getTime():R{0,86399}
        return  0;
      end
      
      function config.hasValidTime():Boolean
        return False;
      end

      transition 
        Top to ShowTime by control.showTime( seconds: R{0,86399} ) do
          timeDisp.time( seconds / (60*60), (seconds / 60) mod 60, seconds mod 60 );
        end

        Top to SetTime by control.setTime( seconds: R{0,86399} ) do
          shour   := seconds / (60*60);
          smin    := (seconds / 60) mod 60;
          timeDisp.time( shour, smin, seconds mod 60 );
        end

        SetTime to ShowTime by control.showTime( seconds: R{0,86399} ) do
          timeDisp.time( seconds / (60*60), (seconds / 60) mod 60, seconds mod 60 );
        end


      state ShowTime
        transition 
          ShowTime to ShowTime by time.time( seconds: R{0,86399} ) do
            timeDisp.time( seconds / (60*60), (seconds / 60) mod 60, seconds mod 60 );
          end
      end

      state SetTime( Visible )
        entry
          write := False;
        end
        
        var
          write:    Boolean;
        
        function config.getTime():R{0,86399}
          if write then
            return  ((shour*60)+smin)*60;
          else
            return 0;
          end
        end
        
        function config.hasValidTime():Boolean
          return True;
        end
        
        transition 
          SetTime to SetTime by hour.evt() do
            if not write then
              write := True;
            end
              
            shour := (shour + 1) mod 24;
            timeDisp.time( shour, smin, 0 );
          end

          SetTime to SetTime by minute.evt() do
            if not write then
              write := True;
            end

            smin := (smin + 1) mod 60;
            timeDisp.time( shour, smin, 0 );
          end
        
        state Visible
          entry
            timer   := 80;
          end
          var
            timer   : R{0,80};
          transition 
            Visible to Visible by tick.evt() if timer > 0 do
              timer := R{0,80}( timer - 1 );
            end
        end

        state Hidden
          entry
            timer   := 20;
            visible.switch( False );
          end
          exit
            visible.switch( True );
          end
          var
            timer   : R{0,20};
            
          transition 
            Hidden to Hidden by tick.evt() if timer > 0 do
              timer := R{0,20}( timer - 1 );
            end
        end
        
      transition 
        Visible to Hidden by tick.evt() if timer = 0;
        Hidden to Visible by tick.evt() if timer = 0;
      end
    end
  end
  
